plugins {
    id 'org.springframework.boot' version '2.4.4'
    id 'io.spring.dependency-management' version '1.0.10.RELEASE'
    id 'java'
    id "org.sonarqube" version "3.1.1"
    id 'jacoco'
    id 'com.adarshr.test-logger' version '3.0.0'
}

group = 'br.com.rafaelshayashi'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

repositories {
    mavenCentral()
}

ext {
    set('springCloudVersion', "2020.0.2")
    set('springDocVersion', '1.5.8')
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'
    implementation 'org.springframework.cloud:spring-cloud-starter-config'
    implementation 'org.springframework.cloud:spring-cloud-starter-bootstrap'
    implementation "org.springdoc:springdoc-openapi-ui:${springDocVersion}"
    implementation "org.springdoc:springdoc-openapi-security:${springDocVersion}"

    // Database
    implementation 'org.flywaydb:flyway-core'
    runtimeOnly 'org.postgresql:postgresql'

    // Tools
    developmentOnly 'org.springframework.boot:spring-boot-devtools'

    // Tests
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'org.springframework.cloud:spring-cloud-contract-wiremock'
    testImplementation 'org.bitbucket.b_c:jose4j:0.7.2'
    testRuntimeOnly 'com.h2database:h2'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}


test {
    useJUnitPlatform()
    testlogger {
        theme 'mocha-parallel'
        showExceptions true
        slowThreshold 10
        showSummary true
        showPassed true
        showSkipped true
        showFailed true
        showStandardStreams false
        showPassedStandardStreams true
        showSkippedStandardStreams true
        showFailedStandardStreams true
    }
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test
}

sonarqube {
    properties {
        property "sonar.projectKey", "library-ddd-by-example-spring-mvc-catalogue"
        property "sonar.organization", "rafaelshayashi"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}

jacoco {
    toolVersion = "0.8.5"
    reportsDirectory = layout.buildDirectory.dir("jacoco")
}

jacocoTestReport {
    reports {
        xml.required = true
        html.required = true
    }
}

// Gradle lockfile for improving Snyk scan
dependencyLocking() {
    lockAllConfigurations()
}

task resolveAndLockAll {
    doFirst {
        assert gradle.startParameter.writeDependencyLocks
    }
    doLast {
        configurations.findAll {
            it.canBeResolved
        }.each {it.resolve()}
    }
}